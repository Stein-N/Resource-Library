plugins {
    id 'ml-loader'
    id 'net.neoforged.gradle.userdev' version '7.0.142'
}

var at = file('src/main/resources/META-INF/accesstransformer.cfg')
if (at.exists()) minecraft.accessTransformers.file at

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"
}

runs {
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', mod_id
        modSource project.sourceSets.main
    }
    data {
        programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated').getAbsolutePath(), '--existing', project(':common').file('src/main/resources/').getAbsolutePath()
        modSource project.sourceSets.main
    }
}

publishMods {
    file = jar.archiveFile
    modLoaders.add("neoforge")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().dependencies.each { node ->
                    node.replaceNode {}
                }
            }
        }
    }
}